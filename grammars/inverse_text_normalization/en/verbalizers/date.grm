import 'common/byte.grm' as b;
import 'common/util.grm' as u;

# Comment:
#   This is an acceptor.
# Example:
#   january ==> january
graph_month = StringFile['data/months.tsv'];

graph_m_unstructured = (
  u.Delete["month: \""]
  graph_month (("" <0.1>) | "-")
  u.Delete["\" day: \""]
);

# Example:
#   day: "5th-" month: "may  ==> 5th-may
graph_dm_unstructured = (
  u.Delete["day: \""]
  b.kDigit+ (("" <0.1>) | "th" | "st" | "nd" | "rd") (("" <0.1>) | "-")
  u.Delete["\" month: \""]
  graph_month
);

# Example:
#   month: "may-" day: "5th ==> may-5th
graph_md_unstructured = (
  u.Delete["month: \""]
  graph_month (("" <0.1>) | "-")
  u.Delete["\" day: \""]
  b.kDigit+ (("" <0.1>) | "th" | "st" | "nd" | "rd")
);

# Example:
#   date { day: "5th-" month: "may-" year: "2012" } ==> 5th-may-2012
#   date { month: "may-" day: "5th-" year: "2012" } ==> may-5th-2012
export graph_date_unstructured = Optimize[
  u.Delete["date { "]
  (
    graph_dm_unstructured
    | graph_md_unstructured
    | graph_m_unstructured
    | u.Delete["month: \"\" day: \""]
  )
  (("" <0.1>) | "-")
  u.Delete["\" year: \""]
  b.kDigit* (("" <0.1>) | "s")
  u.Delete["\" }"]
];
